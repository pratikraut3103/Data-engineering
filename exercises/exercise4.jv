pipeline temperaturePipeline{

    block temperatureZipExtractor oftype HttpExtractor {
        url: "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip";
    }

    block ZipArchiveInterpreter oftype ArchiveInterpreter {
        archiveType: "zip";
    }

    block temperatureFilePicker oftype FilePicker {
        path: "/data.csv";
    }

    block TemperatureTextFileInterpreter oftype TextFileInterpreter {
        encoding: "latin6";
    }

    block temperatureCSVInterpreter oftype CSVInterpreter {
        delimiter: ";";
    }

     block temperatureDataSelector oftype CellRangeSelector {
        select: range A1:J*;
    }

    block temperatureColumnDeleter oftype ColumnDeleter {
        delete: [column F, column G, column H, column I];
    }

    block TemperatureColRename oftype CellWriter {
        at: range  A1:  F1;
        write: ['id','producer','model','month','temperature','battery_temperature'];
    }

    valuetype greater_zero oftype integer {
        constraints: [greater_than_zero];
    }

    constraint greater_than_zero on integer:
        value > 0;

    block TemperatureInterpreter oftype TableInterpreter {

        header: true;
        columns:[
            "id" oftype greater_zero,
            "producer" oftype text,
            "model" oftype text,
            "month" oftype integer,
            "temperature" oftype decimal,
            "battery_temperature" oftype decimal
        ];

    }

    transform CelsiusToFahrenheit {
        from Celsius oftype decimal;
        to Fahrenheit oftype decimal;
        Fahrenheit: (Celsius * 9/5) + 32;
    }

    block CelsiusToFahrenheitTransformer_temp oftype TableTransformer {
    inputColumns: ['temperature'];
    outputColumn: 'temperature';
    use: CelsiusToFahrenheit;
    }

    block CelsiusToFahrenheitTransformer_batt_temp oftype TableTransformer {
    inputColumns: ['battery_temperature'];
    outputColumn: 'battery_temperature';
    use: CelsiusToFahrenheit;
    }

    

    block TemperatureLoader oftype SQLiteLoader {
		table: "temperatures";
		file: "./temperatures.sqlite";
	}

    temperatureZipExtractor
    ->ZipArchiveInterpreter
    ->temperatureFilePicker
    ->TemperatureTextFileInterpreter
    ->temperatureCSVInterpreter
    ->temperatureDataSelector
    ->temperatureColumnDeleter
    ->TemperatureColRename
    ->TemperatureInterpreter
    ->CelsiusToFahrenheitTransformer_temp
    ->CelsiusToFahrenheitTransformer_batt_temp
    ->TemperatureLoader;
}